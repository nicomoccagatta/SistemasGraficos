<html>

<head>
<title>TP Sistemas Gr&aacute;ficos</title>
<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

<script type="text/javascript" src="js/gl-matrix.js"></script>
<script type="text/javascript" src="js/webgl-utils.js"></script>
<script type="text/javascript" src="js/keyboard-and-mouse-support.js"></script>
<script type="text/javascript" src="js/Column.js"></script>
<script type="text/javascript" src="js/Cylinder.js"></script>
<script type="text/javascript" src="js/Road.js"></script>
<script type="text/javascript" src="js/River.js"></script>
<script type="text/javascript" src="js/Arc.js"></script>
<script type="text/javascript" src="js/Field.js"></script>
<script type="text/javascript" src="js/Bridge.js"></script>
<script type="text/javascript" src="js/Trees.js"></script>
<script type="text/javascript" src="js/Tree.js"></script>
<script type="text/javascript" src="js/TexturedSphere.js"></script>
<script type="text/javascript" src="js/initBuffers.js"></script>
<script type="text/javascript" src="js/shaderProgram.js"></script>
<script type="text/javascript" src="js/drawProgram.js"></script>
<script type="text/javascript" src="js/Vertice.js"></script>
<script type="text/javascript" src="js/bufferVertices.js"></script>
<script type="text/javascript" src="js/Grid.js"></script>
<script type="text/javascript" src="js/UpperField.js"></script>
<script type="text/javascript" src="js/ArcField.js"></script>
<script type="text/javascript" src="js/LowerField.js"></script>
<script type="text/javascript" src="js/BSpline.js"></script>

<script id="shader-fs" type="x-shader/x-fragment">
    precision mediump float;

    varying vec4 vTextureCoord;
    varying vec3 vPosition;
    varying vec3 vmPosition;
    varying vec3 vNormal;
    varying vec3 vTangent;
    varying vec3 vBinormal;

    varying float vUseReflection;
    uniform bool uUseColor;
    uniform bool uUseNormal;
    uniform bool uUseIlumination;
    uniform float uIluminationIntensity;

    uniform vec3 uCameraPosition;
    varying vec3 light_dir_normalized;
    varying vec3 view_dir_normalized;

    uniform sampler2D uSampler;
    uniform sampler2D uSamplerReflectionMap;
    uniform sampler2D uSamplerNormalMap;
    uniform sampler2D uSamplerIluminationMap;

    uniform vec3 uAmbientColor;
    uniform bool uUseLighting;
    uniform bool uUsePunctualLights;

    // Sol: Direccional
    uniform vec3 uPrincipalLightDirection;
    uniform vec3 uPrincipalDiffuseColor;
    uniform vec3 uPrincipalSpecularColor;
    uniform float uPrincipalLightIntensity;

    void main(void) {
        vec3 normal = normalize(vNormal);

        // Normal Map
        if (uUseNormal) {
            vec3 tangente = normalize(vTangent);
            vec3 binormal = normalize(vBinormal);

            // Matriz para pasar de coordenadas de vista a coordenadas intrinsecas (coordenadas para hacer el normal mapping)
            mat3 toLocalObject = mat3(tangente, binormal, normal);
            vec3 nMap         = texture2D(uSamplerNormalMap, vec2(vTextureCoord.s, vTextureCoord.t)).rgb;
            vec3 normalMap    = (nMap * 2.0) - 1.0;
            normal            = normalize(toLocalObject * normalMap);
        }

        // Calculos de la iluminación
        // Sol
        vec3 dir_luz_norm = normalize(uPrincipalLightDirection - vmPosition);
        vec3 dir_vista_norm = normalize(uCameraPosition - vmPosition);
        vec3 halfVector = normalize(dir_luz_norm + dir_vista_norm);

        vec3 lightDir = uPrincipalLightDirection;
        vec3 lightDirNormalized = normalize(lightDir);
        float diffusePrincipalLightWeighting = max(dot(lightDirNormalized,normal), 0.0);
        float specularPrincipalLightWeighting = pow(max(dot(normal, halfVector),0.0),64.0);
     
        if (! uUseLighting) {
            diffusePrincipalLightWeighting = 0.0;
            specularPrincipalLightWeighting = 0.0;
        }

        vec3 vPrincipalLightWeighting = uPrincipalLightIntensity * (uAmbientColor +
                                                                    uPrincipalDiffuseColor * diffusePrincipalLightWeighting +
                                                                    uPrincipalSpecularColor * specularPrincipalLightWeighting);
                                                                    
        vec3 vLightWeighting = vPrincipalLightWeighting;

        // Utilizo color o textura
        vec4 textureColor;
        if (uUseColor) {
            textureColor = vec4(vTextureCoord.rgb, 1.0);
        } else {
            textureColor = texture2D(uSampler, vec2(vTextureCoord.s, vTextureCoord.t));
        }
        gl_FragColor = vec4(textureColor.rgb * vLightWeighting, textureColor.a);

        // Si utiliza reflexion
        if (vTextureCoord[3] == 2.0 || vUseReflection == 1.0) {
            vec3 view = normalize(vPosition);
            vec3 reflection = reflect(view, vNormal);
            float m = 2.0 * sqrt( pow(reflection.x, 2.0) + pow(reflection.y, 2.0) + pow(reflection.z + 1.0, 2.0));
            vec2 reflectionDir = reflection.xy / m + 0.5;
            vec4 reflectionTexture = texture2D(uSamplerReflectionMap, reflectionDir);
            gl_FragColor = vec4(mix(textureColor, reflectionTexture, 0.2).rgb * vLightWeighting, textureColor.a);
        }
    }
</script>

<script id="shader-vs" type="x-shader/x-vertex">
    attribute vec3 aVertexPosition;
    attribute vec3 aVertexNormal;
    attribute vec3 aVertexTangent;
    attribute vec3 aVertexBinormal;
    attribute vec4 aTextureCoord;

    uniform mat4 uViewMatrix;
    uniform mat4 uModelMatrix;
    uniform mat4 uProyMatrix;
    uniform mat3 uNormalMatrix;

    uniform vec3 uPunctual1LightPosition;
    uniform vec3 uPunctual2LightPosition;
    uniform vec3 uPunctual3LightPosition;
    uniform vec3 uPunctual4LightPosition;

    uniform vec3 uEyePoint;
    uniform vec3 uSunPosition;

    varying vec4 vTextureCoord;
    varying vec3 vPosition;
    varying vec3 vmPosition;
    varying vec3 vNormal;
    varying vec3 vTangent;
    varying vec3 vBinormal;

    varying vec3 light_dir_normalized;
    varying vec3 view_dir_normalized;

    uniform float uUseReflection;
    varying float vUseReflection;

    void main(void) {
        // Transformamos al vértice al espacio de la cámara
        vec4 pos_camera_view = uViewMatrix * uModelMatrix * vec4(aVertexPosition, 1.0);

        // Transformamos al vértice al espacio de la proyección
        gl_Position = uProyMatrix * pos_camera_view;

        vPosition = vec3(pos_camera_view) / pos_camera_view.w;

        // Coordenada de textura sin modificaciones
        vTextureCoord = aTextureCoord;

        // Para normalMap
        vNormal = normalize(uNormalMatrix * aVertexNormal);
        vTangent = normalize(uNormalMatrix * aVertexTangent);
        vBinormal = normalize(uNormalMatrix * aVertexBinormal);

        vmPosition = (uModelMatrix * vec4(aVertexPosition, 1.0)).xyz;

        light_dir_normalized = normalize(uSunPosition - vmPosition);
        view_dir_normalized = normalize(uEyePoint - vmPosition);

        // Indica si usa reflection.
        vUseReflection = uUseReflection;
    }
</script>

<script type="text/javascript">

    var gl;

    var CameraMatrix = mat4.create();
    var mvMatrix = mat4.create();
    var mvMatrixStack = [];
    var pMatrix = mat4.create();

    var mouseDown = false;
    var lastMouseX = null;
    var lastMouseY = null;

    function degToRad(degrees) {
        return degrees * Math.PI / 180;
    }

    // Variables de los shaders
    var shaderProgramSimple;

</script>

</head>

<body onload="webGLStart();">
<style>

    body {
      font: 13px sans-serif;
      position: relative;
      width: 960px;
      height: 500px;
    }

    form {
      position: absolute;
      background-color: lightgrey;
      bottom: -200px;
      left: 1000px;
    }

    rect {
      fill: none;
      pointer-events: all;
    }

    circle,
    .line {
      fill: none;
      stroke: blue;
      stroke-width: 5.0px;
    }

    circle {
      fill: #fff;
      fill-opacity: .2;
      cursor: move;
    }

    .selected {
      fill: #ff7f0e;
      stroke: #ff7f0e;
    }
</style>

<form>
</form>

<script src="js/d3.v3.min.js"></script>
<script src="js/points.controller.js"></script>

<script src="js/dat.gui.min.js"></script>
<script src="js/dinamicValues.js"></script>

    <center>
            <canvas id="TP Sistemas Graficos" onmousemove="onMouseMove(event)" onmousedown="onMouseDown(event)" onmouseup="onMouseUp(event)" 
                    style="border: none;" width="1280" height="720">
            Your browser does not support the HTML5 canvas element.
            </canvas>
    </center>
 </body>

</html>
